<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
	creationComplete="onCreationComplete()"
	 keyDown="onKeyDown(event);"
	 verticalScrollPolicy="off"
	 horizontalScrollPolicy="off" >
    <mx:Script source="Activity3DChartAction.as" />
	<mx:Script>
		<![CDATA[
		import org.papervision3d.core.geom.Lines3D;
		import org.papervision3d.materials.special.LineMaterial;
		import org.papervision3d.core.geom.renderables.Line3D;
		import org.papervision3d.core.geom.renderables.Vertex3D;
		import org.papervision3d.materials.shadematerials.GouraudMaterial;
		import org.papervision3d.lights.PointLight3D;
		import org.papervision3d.core.proto.MaterialObject3D;
		import org.papervision3d.typography.fonts.HelveticaBold;
		import mx.messaging.channels.StreamingAMFChannel;
		import org.papervision3d.typography.Text3D;
		import org.papervision3d.typography.fonts.HelveticaLight;
		import org.libspark.utils.ColorUtil;
		import org.papervision3d.materials.special.Letter3DMaterial;
		import org.papervision3d.objects.special.ParticleField;
		import org.papervision3d.materials.special.ParticleMaterial;
		import org.papervision3d.core.render.sort.NullSorter;
		import models.events.HoneypotEventMessage;
		import models.graphs.Activity3DChartNode;
		import models.graphs.ActivityTimeDict;
		import models.graphs.ActivityCube;
		import org.papervision3d.render.BasicRenderEngine;
		import org.papervision3d.materials.utils.MaterialsList;
		import org.papervision3d.materials.special.CompositeMaterial;
		import org.papervision3d.scenes.*;
		import org.papervision3d.objects.*;
		import org.papervision3d.objects.primitives.*;
		import org.papervision3d.cameras.*;
		import org.papervision3d.materials.*;
		import org.papervision3d.view.Viewport3D;
		import org.papervision3d.events.InteractiveScene3DEvent;
		import org.papervision3d.scenes.Scene3D;
		import mx.core.UIComponent;
		import controllers.ActivityChartManager;
		import models.utils.Logger;

		import mx.charts.series.LineSeries;
		import mx.collections.ArrayCollection;


		private var container : UIComponent;
		private var viewport : Viewport3D;
		private var scene : Scene3D;
		private var camera : Camera3D;
		private var rootNode : DisplayObject3D;
		private var renderer:BasicRenderEngine;
		
		private var rootNodeDefaultRotationX:Number = -90;
		private var rootNodeDefaultRotationY:Number = 90;
		private var rootNodeDefaultRotationZ:Number = 0;
		
		private var startX : Number = 0;
		private var startY : Number = 0;
		
		[Bindable]
		private var zoom:Number = 5;
		private var maxZoom : Number = 100;
		private var minZoom : Number = 1;
		
		[Bindable]
		private var cameraX:Number;
		[Bindable]
		private var cameraY:Number;
		[Bindable]
		private var cameraZ:Number;
		
		private var axisLength : Number = 150;
			
 		private var floor:Plane;
 		private var basicMaterial:MaterialsList;

		private var timeWall:Plane;
		private var timeWallWidth:Number = 2000;
		private var timeWallHeight:Number = 10;
		private var timeMulNum:Number;
		private var timeRatio:Number = 300.0;
		private var lastTimeWallMoved:Number;
		private var timeWallPeriod:Number = 1000;
		
		private var activityPeriod:Number = 10000;// milliseconds
		
		private var cameraInitPositionX:Number = 4000;
		private var cameraInitPositionY:Number = 4000;
		private var cameraInitPositionZ:Number = 4000;
		private var cameraInitFocus:Number = 2000;
		private var cameraInitZoom:Number = 3;
		
		private var moveRatio:Number = 3.0;
 		private var timeDict:ActivityTimeDict;
 		
 		private var nodeLabelFont:HelveticaLight = new HelveticaLight;
 		private var labelMaterials:Array = new Array;
 		private var labelMaterialsIndex:int = 0;
 		
 		private var cubeMaterials:Array = new Array;
		private var light:PointLight3D;
		
		public static var infoLayer:UIComponent = new UIComponent;

		private function onCreationComplete():void
		{
			container = new UIComponent;
			canvas.addChild(container);
			
			canvas.addChild(infoLayer); 
			this.parent.height = canvas.height;
			container.width = canvas.width;
			container.height = canvas.height;
						
			init3D();

			addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
			addEventListener(MouseEvent.MOUSE_UP,   onMouseUp);
			addEventListener(MouseEvent.MOUSE_WHEEL, onMouseWheel);
		}
		
		private function onCanvasComplete():void
		{
		}
		
		private function setMask():void
		{
			var m:Sprite = new Sprite();
			var g:Graphics = m.graphics;
			var cvPt:Point = canvas.contentToGlobal(new Point(0, 0));
			var pt:Point = m.globalToLocal(cvPt);
			g.lineStyle(0, 0, 0);
			g.beginFill(0xFF);
			g.drawRect(pt.x, pt.y, canvas.width, canvas.height);
			g.endFill();
			canvas.mask = m;
		}
		private function initLight():void
		{
			light = new PointLight3D();
			light.z = 0
			light.y = 500;
			light.x = 0;
		}

		private function initTimeWall():void
		{
			var coMaterial1:ColorMaterial = new ColorMaterial(0xFF9999, 0.3);
			var timeWallMaterial:CompositeMaterial = new CompositeMaterial();
			timeWallMaterial.addMaterial(coMaterial1);
			timeWallMaterial.doubleSided = true;

			timeWall = new Plane(timeWallMaterial, timeWallWidth, timeWallHeight);
			rootNode.addChild(timeWall);
			timeWall.y = 0;
			timeWall.x = timeWallWidth/2;
			timeWall.z = timeWallHeight/2;
			timeWall.rotationX = 90;
			lastTimeWallMoved = 0;
		}
		
		
		private function moveTimeWall(time:Number):void
		{
			timeWall.y = time / timeRatio;
			render();
		}
		
		public var count:uint = 0;
		public function seekByTime(time:Number):void
		{
			if (time < lastTimeWallMoved) {
				lastTimeWallMoved = uint(time / timeWallPeriod) * timeWallPeriod;
				return;
			}
			if (time >= lastTimeWallMoved + timeWallPeriod) {
				moveTimeWall(time);
				lastTimeWallMoved = uint(time / timeWallPeriod) * timeWallPeriod;
				highlightNodesByTime(lastTimeWallMoved);
			}
		}
		
		private var floorWidth:Number = 2000;
		private var floorDepth:Number = 2000;
		/*
			Quioted http://blog.livedoor.jp/takaaki_bb/archives/50614075.html
		*/
		private function init3D():void
		{
			var c:UIComponent = new UIComponent;
			container.addChild(c);
			c.width = container.width;
			c.height = container.height;
			viewport = new Viewport3D(c.width, c.height, true, true);
			c.addChild(viewport);

			scene = new Scene3D();
			
			
				
			renderer = new BasicRenderEngine();
			
			
			/*
				Camera configuration
			*/
			camera = new Camera3D;

			rootNode = scene.addChild(new DisplayObject3D("rootNode"));
			rootNode.x = 250;
			rootNode.y = 0;
			resetCamera();
			drawAxises();
			
			camera.lookAt(DisplayObject3D.ZERO);
			drawFloor(floorWidth, floorDepth);
			initTimeWall();
			initParticles();
			
			Activity3DChartNode.setHighlightMaterial(0xFFFFFF);
			//drawXYPlane(1000, 1000);


			render();
 		}
 		
 		private function drawAxises():void
 		{
/*
			var xAxis:DisplayObject3D = drawAxis(0xFF0000);
			xAxis.yaw(90);
			var yAxis:DisplayObject3D = drawAxis(0x00FF00);
			yAxis.pitch(-90);
			var zAxis:DisplayObject3D = drawAxis(0x0000FF);
			
			rootNode.addChild(xAxis);
			rootNode.addChild(yAxis);
			rootNode.addChild(zAxis);
*/
			var lines:Lines3D = new Lines3D;
			var lx:Line3D = createLine(lines, 0xCC0033, floorWidth, 0, 0);
			var ly:Line3D = createLine(lines, 0x33CC00, 0, floorDepth, 0);
			var lz:Line3D = createLine(lines, 0x0033CC, 0, 0, 300);
			lines.addLine(ly);
			lines.addLine(lz);
			lines.addLine(lx); 
            rootNode.addChild(lines);

 		}
 		
 		private function drawNodeLabels():void
 		{
 			var nodeName:String;
 			var i:int = 0;
 			var l:int = labelMaterials.length;
 			var m:Letter3DMaterial;
 			
 			for (nodeName in _nodeNameIndexDict) {
 				i = getNodeIndex(nodeName);
 				m = labelMaterials[i % l];
 				
 				drawNodeLabelOnAxis(i * nodeWidth, m, nodeName); 
 			}
 		}
 		
 		
 		
 		private function drawNodeLabelOnAxis(positionX:Number,
 											 material:Letter3DMaterial, text:String):void
 		{
			if (labelMaterials.length == 0) {
				Logger.log("invalid call sequence");
				return;
			}
			var w:Text3D = new Text3D(text, nodeLabelFont, material);
			w.letterSpacing = -10;
			w.lineSpacing = -10;
			w.x = positionX;
			w.y = -30;
			w.rotationZ -= 90;
			w.rotationY -= 180;
			w.scaleY = w.scaleX = 0.1;
			rootNode.addChild(w);
 		}
 		
 		
 		
 		private function prepareNodeLabelMaterials():void
 		{
 			var m:Letter3DMaterial;
 			var c:uint;
 			var nodeName:String;
 			for (nodeName in _nodeNameIndexDict) {
 				c = getColorOfNodeName(nodeName);
 				m = new Letter3DMaterial(c, 1);
 				m.doubleSided = true;
 				labelMaterials.push(m);
 			}
 		}
 		
 		private var nodeColors:Array = new Array;
 		private var nodeDarkColors:Array = new Array;
 		
 		private function prepareNodeColors():void
 		{
 			var c:uint;
 			nodeColors.length = nodeDarkColors.length = 0;
 			for (var r:int = 0; r < 360; r += 30) {
 				c = org.libspark.utils.ColorUtil.hsv(r, 0.5, 0.9);
 				nodeColors.push(c);
 				c = org.libspark.utils.ColorUtil.hsv(r, 1, 0.3);
 				nodeDarkColors.push(c);
 			}
 		}
 		
 		private function getColorOfNodeName(nodeName:String):uint
 		{
 			var c:uint;
 			var i:int = getNodeIndex(nodeName);
 		
 			c = nodeColors[i%nodeColors.length];
 			return c;
 		}
 		
 		private function getDarkColorOfNodeName(nodeName:String):uint
 		{
 			var i:int = getNodeIndex(nodeName);
 			var c:uint = nodeDarkColors[i%nodeColors.length];
 			return c;
 		}
 		
 		private function prepareBasicMaterials():void
 		{
			var color:uint = 0xFF9933;
			var colorMaterial:ColorMaterial = new ColorMaterial(color, 1);
			var wireMaterial:WireframeMaterial = new WireframeMaterial(0xDDDDDD);
			
			var compoMaterial:CompositeMaterial = new CompositeMaterial();
			compoMaterial.addMaterial(wireMaterial);
			compoMaterial.addMaterial(colorMaterial);
			compoMaterial.addMaterial(wireMaterial);
			compoMaterial.doubleSided = true;
			
			var materialsList:MaterialsList = new MaterialsList();
			compoMaterial.interactive = true;
			materialsList.addMaterial(compoMaterial, "all");
			basicMaterial = materialsList;
			
			
			var nodeName:String;
			var c:uint;
			var c2:uint;
			var a:Object = new Object;
			var g:GouraudMaterial;
			for (nodeName in _nodeNameIndexDict) {
				c = getColorOfNodeName(nodeName);
				c2 = getDarkColorOfNodeName(nodeName);
				colorMaterial = new ColorMaterial(c, 1);
//				g = new GouraudMaterial(light, c, c2);
				compoMaterial = new CompositeMaterial();
				compoMaterial.addMaterial(wireMaterial);
				compoMaterial.addMaterial(colorMaterial);
				compoMaterial.doubleSided = true;
				compoMaterial.interactive = true;
				var ml:MaterialsList = new MaterialsList({all:compoMaterial});
				cubeMaterials.push(ml);
			}
 		}
 		
 		private function getMaterialofNodeName(nodeName:String):MaterialsList
 		{
 			var i:int = getNodeIndex(nodeName);
 			return cubeMaterials[i % cubeMaterials.length];
 		}
 		
 		private function initParticles():void
 		{
 			var particle:ParticleMaterial = new ParticleMaterial(0xffffff, 1,
 				ParticleMaterial.SHAPE_CIRCLE);
 			var particles:ParticleField = new ParticleField(particle, 1000, 2, 2000,2000,2000);
 			rootNode.addChild(particles);
 		}
 		
 		public function drawFloor(width:Number, height:Number):void
 		{
			var coMaterial1:ColorMaterial = new ColorMaterial(0xDDDDDD, 0.4);

			var floorMaterial:CompositeMaterial = new CompositeMaterial();
			floorMaterial.addMaterial(coMaterial1);
			floorMaterial.doubleSided = true;

			floor = new Plane(floorMaterial, width, height);
			rootNode.removeChild(floor);
			rootNode.addChild(floor);
			floor.rotationZ = 180;
			floor.y = height / 2;
			floor.x = width / 2;
 			
 		} 
 		
 		/*
 			Gets honeypotEventMessages and draws
 		*/
 		public function initGraph(messages:Array):void
 		{
 			var msg:HoneypotEventMessage;
 			var hostNames:Array = new Array;
 			for each(msg in messages) {
 				hostNames.push(msg.hostname ? msg.hostname : msg.host1);
 			}
 			prepareNodeIndex(hostNames);
 			prepareNodeColors();
			prepareBasicMaterials();
 			/*
 				time => array of Activity3DChartnode
 			*/
 			
 			
 			timeDict = processHoneypotMessages(messages, activityPeriod);
 			var a:Array;
 			var t:Number;
 			var timeArray:Array = timeDict.keys();
 			for each(t in timeArray) {
 				a = timeDict.getMessagesByTime(t);
 				drawNodesByTime(t, a);
 			}
 			prepareNodeLabelMaterials();
 			drawNodeLabels(); 			
 			render();    
 		}
 		
 		
 		private function drawNodesByTime(time:Number, nodes:Array):void {
 			var n:Activity3DChartNode;
 			for each (n in nodes) {
 				drawNodeByTime(time, n);
 			}
 		}
 		
 		private var _nodeNameIndexDict:Object = new Object;
 		private function prepareNodeIndex(nodeNames:Array):void {
 			var nodeName:String;
 			var i:int = 0;
 			for each(nodeName in nodeNames) {
 				if (_nodeNameIndexDict[nodeName] == null) {
 					_nodeNameIndexDict[nodeName] = i;
 					i++;
 				}
 			}
 		}
 		
 		private function getNodeIndex(nodeName:String):int {
 			var i:int;
 			if (_nodeNameIndexDict[nodeName] == null) {
 				Logger.log("No such node index");
 				return -1;
 			}
 			return _nodeNameIndexDict[nodeName];
 		}

 		/*
 			Draws a node
 		*/
 		
 		private static var count:int = 0;
 		private var nodeWidth:Number = 20;
 		private var nodeDepth:Number = 20;
 		private var nodeHeightRatio:Number = 10;
 		private function drawNodeByTime(time:Number, node:Activity3DChartNode):void
 		{
 			var m:MaterialsList = this.getMaterialofNodeName(node.hostName);
			var cube:ActivityCube = new ActivityCube(node, time, m, nodeWidth,
												node.activity * nodeHeightRatio + 10,nodeDepth);
 			cube.x = getNodeIndex(node.hostName) * nodeWidth;
 			cube.y = time / timeRatio;
 			cube.z = node.activity * nodeHeightRatio / 2;
 			cube.node = node;
 			cube.time = time;
            cube.addEventListener(InteractiveScene3DEvent.OBJECT_OVER,
            					  onCubeOvered);
			node.view = cube;
			rootNode.addChild(cube);
 		}
 		
 		private function onCubeClicked(event:InteractiveScene3DEvent):void
 		{
 			var t:ActivityCube = event.displayObject3D as ActivityCube;
 			var name:String = t.node.hostName;
			event.stopPropagation();
 		}
 		
 		private function onCubeOvered(event:InteractiveScene3DEvent):void
 		{
 			var t:ActivityCube = event.displayObject3D as ActivityCube;
 			var name:String = t.node.hostName;
 		}
  		
 		private function highlightNodes(nodes:Array):void
 		{
 			var node:Activity3DChartNode;
 			for each (node in nodes) {
 				node.highlightView();
 			}
 		}
 		
 		private function highlightNodesByTime(time:Number):void
 		{
 			var a:Array = timeDict.getMessagesByTime(time);
			if (a != null) {
				Activity3DChartNode.resetChangedNodes()
				highlightNodes(a);
			}
 		}
 		
 		import flash.events.Event;
 		private function onMouseDown(event:MouseEvent):void {
            startX = event.stageX;
            startY = event.stageY;
            
            
            //addEventListener(Event.ENTER_FRAME, onMove);
            addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
 		}
 		
 		
 		private function onMouseMove(event:MouseEvent):void {
 			if (moveRootNode) {
	            // 順番気にせず回転させる場合
	            rootNode.yaw(- (event.stageX - startX));
	            rootNode.pitch(event.stageY - startY);
	            
	            // 順番に回転させる場合
	            rootNode.rotationZ -= (event.stageY - startY);
	            rootNode.rotationY -= (event.stageX - startX);
	            
	            // 直感的に(画面上のx軸、y軸に沿って)回転させる方法はどうやるんだろう
	            startX = event.stageX;
	            startY = event.stageY;
 				
 			} else {
 				var xd:Number = (event.stageX - startX) / moveRatio;
 				var yd:Number = (event.stageY - startY) / moveRatio;
 				if (event.stageX < 50 || event.stageY < 50) return;
            	camera.x -= xd;
            	camera.z += yd;
            	
			}
            // レンダリングしなおす
            render();
        }

		private function onMouseWheel(e:MouseEvent):void
		{
			if ((camera.zoom <= maxZoom && camera.zoom >= minZoom)
				|| (camera.zoom >= maxZoom && e.delta < 0)
				|| (camera.zoom <= minZoom && e.delta > 0)) {
				camera.zoom += e.delta / 5.0;
			}
			if (camera.zoom < 1) { 
				camera.zoom = 1;
			}
			render();
		}
		
		private function onMouseUp(event:MouseEvent):void {
            removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
            //removeEventListener(Event.ENTER_FRAME, onMove);
            this.setFocus();
        }
                
        private function resetCamera():void
        {
        	rootNode.rotationX = rootNodeDefaultRotationX;
        	rootNode.rotationY = rootNodeDefaultRotationY;
        	rootNode.rotationZ = rootNodeDefaultRotationZ;
        	
    		camera.x = cameraInitPositionX;
    		camera.y = cameraInitPositionY;
    		camera.z = cameraInitPositionZ;
    		camera.zoom = cameraInitZoom;
			camera.focus = cameraInitFocus;
        }
        
        private function render():void
        {
        	//レンダリング
            renderer.renderScene(scene, camera, viewport);
            zoom = int(camera.zoom);
            cameraX = int(camera.x);
            cameraY = int(camera.y);
            cameraZ = int(camera.z);
            canvas.width = canvas.parent.width;
            canvas.height = canvas.parent.height;
         	setMask();            
        }

		private function drawAxis(color:uint, length:Number = -1):DisplayObject3D {
            // 色設定
            if (length < 0) length = axisLength;
            var colorMaterial:ColorMaterial = new ColorMaterial(color, 1);
            var wireMaterial:WireframeMaterial = new WireframeMaterial(color);
            
            var compoMaterial:CompositeMaterial = new CompositeMaterial();
            compoMaterial.addMaterial(colorMaterial);
            // compoMaterial.addMaterial(wireMaterial);
            compoMaterial.doubleSided = true;
            compoMaterial.interactive = true;
            
            var materialsList:MaterialsList = new MaterialsList();
            materialsList.addMaterial(compoMaterial, "all");
            // 線を引く (円柱だけど)
            var axis:DisplayObject3D = new Cylinder(compoMaterial, 2, length);
            axis.z = 75;
            axis.rotationX = 90;
            
            var node:DisplayObject3D = new DisplayObject3D();
            node.addChild(axis);

            return node;
        }
        
        private function createLine(lines:Lines3D, color:uint, vx:Number, vy:Number, vz:Number):Line3D
        {
			var xs:Vertex3D =new Vertex3D(-vx, -vy, -vz);
			var xe:Vertex3D =new Vertex3D(vx, vy, vz);
			var lm:LineMaterial = new LineMaterial(color, 1);
			var l:Line3D =new Line3D(lines, lm, 2, xs, xe);
			return l;
        }

        private function createAxis(color:uint, length:Number = -1):DisplayObject3D {
        	if (length < 0) length = axisLength;
            var colorMaterial:ColorMaterial = new ColorMaterial(color, 1);
            var axis:DisplayObject3D = new Cylinder(colorMaterial, 2, length);
            return axis;
        }
        
        private var xyPlane:DisplayObject3D;
        
        private function drawXYPlane(xWidth:Number, yWidth:Number, skip:int = 20):void
        {
        	if (xyPlane != null)
        		rootNode.removeChild(xyPlane);
        	xyPlane = new DisplayObject3D;
        	rootNode.addChild(xyPlane);
        	var i:int, j:int;
        	var x:Number = 0, y:Number = 0;
        	var axis:DisplayObject3D;
        	for (i=0; x < xWidth; ++i, x=i*skip) {
        		axis = createAxis(0xFFFFFF, yWidth);
        		axis.yaw(90);
				axis.y = x;
        		xyPlane.addChild(axis);
        	}
			
        	for (j=0; y < yWidth; ++j, y=j*skip) {
        		axis = createAxis(0xFFFFFF, xWidth);
        		axis.pitch(-90);
        		axis.x = y;
        		xyPlane.addChild(axis);
        	}
        }
        
        private function panCamera(angle:Number):void
        {
        	camera.rotationY += angle;
        	render();
        }
        
        [Bindable]
        private var moveRootNode:Boolean = false;
        
        private function onKeyDown(event:KeyboardEvent):void
        {
        	switch (event.keyCode) {
        		case 32: // space key
        			moveRootNode = !moveRootNode;
        			break;
        		case 37:
        			panCamera(+0.1);
        			break;
        		case 39:
        			panCamera(-0.1);
        			break;
        		default:
					break;
        	}
        }
        
        
		]]>
	</mx:Script>
	<mx:Panel title="Activity Line Chart"  width="100%" height="95%" >
		<mx:VBox width="100%" height="100%" >
			<!-- <mx:CheckBox id="drawGraphCheck" label="Draw Graph" selected="{_drawGraph}"
				click="toggleDraw()" /> -->
			<mx:HBox>
				<mx:Text text="Zoom: {zoom}" />
				<mx:Text text=", CameraX: {cameraX}" />
				<mx:Text text=", CameraY: {cameraY}" />
				<mx:Text text=", CameraZ: {cameraZ}" />
				<mx:Text text="Drag: {moveRootNode}" />
				<mx:Button label="Camera Reset" click="resetCamera();" />
				
			</mx:HBox>
			<mx:Canvas id="canvas" width="100%" minWidth="1000" height="100%" minHeight="600" creationComplete="onCanvasComplete();"
				 backgroundColor="0x000000" />
		</mx:VBox>
	</mx:Panel>
</mx:Canvas>
